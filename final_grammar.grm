"Name"     = 'RICHARD'
"Author"   = 'João Victor, Eduardo Marques, Gustavo Henrique, Marcos Aldrey, Marcos Vinicius, Alyson, Alisson, André, Gilvaney, Nielson'
"Version"  = 'The version of the grammar and/or language'
"About"    = 'A short description of the grammar'

"Start Symbol" = <Program>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{String Chars} = {Printable} + {HT} - ["\]

! -------------------------------------------------
! Terminals
! -------------------------------------------------

StringLiteral = '"' ( {String Chars} | '\' {Printable} )* '"'
Identifier   = {Letter}({AlphaNumeric} | '_')*
NumberTerminal       = {Number}+('.'{Number}+)?

! -------------------------------------------------
! Rules
! -------------------------------------------------

! The grammar starts below
<Program>              ::= <Class> | <Constants>

<Expr Arit>            ::= <Mult Exp> '+' <Expr Arit> | <Mult Exp> '-' <Expr Arit> | <Mult Exp> 
<Mult Exp>             ::= <Negate Exp> '*' <Mult Exp> | <Negate Exp> '/' <Mult Exp> | <Negate Exp> 
<Valor Exp>            ::= <Value> | '(' <Expr Arit> ')'
<Negate Exp>           ::= '-' <Valor Exp> | <Valor Exp> |
<Expression>           ::= <Negate Exp> |

!Add more values to the rule below - as needed

<Value>                ::= 'true' | 'false' | StringLiteral | NumberTerminal

<Empty Value>          ::= <Value> |

<Array>                ::= '[' <Expression> ']' <Array>  |
          
<Init Array>           ::= '{' <Init Array_2> '}'
                
<Init Array_2>         ::= '(' <Init Array_3> ')' |  '(' <Init Array_3> ')' <Init Array_2>
                 
<Init Array_3>         ::= <Initial Value> | <Initial Value> ',' <Init Array_3>

<Declaration>          ::= <Type> <Valid Identifier>

<Valid Identifier>     ::= Identifier <Array>

<Type>                 ::= 'string' | 'int' | 'void' | 'boolean' | Identifier
                        
<Attribute Access>     ::= '.' <Valid Identifier> <Attribute Access> |

<Valid Values>         ::= <Value> | <Valid Identifier> <Attribute Access>
                        
<Initial Value>        ::= <Init Array> | <Valid Values> | <Method Call>
                        
<Method Call>          ::= <Valid Identifier> <Attribute Access> '(' <Arguments> ')'

<Multiple Identifier>  ::= ',' <Valid Identifier>

<Attributition>        ::= <Multiple Identifier> '=' <Initial Value> <Attributition> |

<Constant Assignment>  ::= <Declaration> '=' <Initial Value> <Attributition> ';' <Constant Assignment> |

<Initialize>           ::= '=' <Initial Value> |

<Initialize Variable>  ::= <Multiple Identifier> <Initialize> |
                        
<Variable Assignment>  ::=  <Declaration> <Initialize> <Initialize Variable> ';' <Variable Assignment> |

<Constants>            ::= 'constant' '{' <Constant Assignment> '}'
                        
<Variables>            ::= 'variables' '{' <Variable Assignment> '}'
                        
<Extends>              ::= 'extends' Identifier |
                        
<Class Code>           ::= <Variables> <Class Code> | <Methods> <Class Code> |

<Class>                ::= 'class' Identifier <Extends> '{' <Class Code> '}'

<Parameters>           ::= <Declaration> <Parameter>

<Parameter>            ::= ',' <Parameters> |

<Methods>              ::= 'method' <Declaration> '(' <Parameters> ')' <Code Block>

<Code Block>           ::= '{' <Code Statements> '}'

<Code Statements>      ::= <If-Block> <Code Statements> | <Looping-Block> <Code Statements>
                        | <Line Code> <Code Statements> |
                        
<Condition>            ::= <Expression> | <Method Call>
                        
<Arguments>            ::= <Initial Value> | ',' <Initial Value> |

<Line Code>            ::= <Expression> ';' | 'return' <Empty Value> ';' | <Method Call> ';' | <Read> ';' | <Write> ';' | <Expression> ';'
                        
<If-Block>             ::= 'if' '(' <Condition> ')' 'then' <Code Block> <Else-Block>
                       
<Else-Block>           ::= 'else' <Post-Else-Block> |

<Post-Else-Block>      ::= <If-Block> | <Code Block> |
                        
<Looping-Block>        ::= 'while' '(' <Condition> ')' <Code Block>

<Read>                 ::= 'read' '(' <Valid Values> <Read Parameters> ')'
                        
<Read Parameters>      ::= ',' <Valid Values> <Read Parameters> |
                           
<Write>                ::= 'write' '(' <Write Parameters> ')'
                        
<To-Write>             ::= ',' <Valid Values> <To-Write> | ',' <Method Call> <To-Write> |

<Write Parameters>     ::= <Valid Values> <To-Write> | <Method Call> <To-Write>
